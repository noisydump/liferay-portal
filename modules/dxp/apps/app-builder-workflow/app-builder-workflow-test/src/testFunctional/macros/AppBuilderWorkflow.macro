definition {

	macro addNewStep {

		Click(locator1 = "AppBuilderWorkflow#NEW_STEP_BUTTON");
	}

	macro addWorkflowAppName {

		Type(
			locator1 = "AppBuilderWorkflow#WORKFLOW_APP_NAME_FIELD",
			value1 = "${workflowAppName}");
	}

	macro assertButtonDisabled {

		AssertVisible(
			key_text = "${buttonName}",
			locator1 = "Button#ANY_DISABLED");
	}

	macro assertFieldDisabled {

		AssertVisible(
			key_fieldName = "${fieldName}",
			locator1 = "AppBuilderWorkflow#TEXT_FIELD_DISABLED");
	}

	macro assignEntry {

		Click(
			key_itemName = "${itemName}",
			locator1 = "AppBuilder#VERTICAL_ELLIPSIS_BUTTON");

		while (IsElementNotPresent(key_menuItem = "Assign to", locator1 = "MenuItem#ANY_MENU_ITEM")) {

			Refresh();
			Pause(locator1 = "3000");
			Click(
				key_itemName = "${itemName}",
				locator1 = "AppBuilder#VERTICAL_ELLIPSIS_BUTTON");
		}

		MenuItem.clickNoError(menuItem = "Assign to");

		Click(locator1 = "AppBuilderWorkflow#SELECT_NEW_ASSIGNEE_BUTTON");

		Type(
			locator1 = "AppBuilderWorkflow#SELECT_NEW_ASSIGNEE_SEARCH_FIELD",
			value1 = "${assignee}");

		Click(
			key_assignee = "${assignee}",
			locator1 = "AppBuilderWorkflow#SELECT_NEW_ASSIGNEE");

		Button.click(button = "Done");

		Refresh();
	}

	macro clickBackButton {

		Click(locator1 = "AppBuilderWorkflow#BACK_BUTTON");
	}

	macro clickOnSelectFormView {

		Click(locator1 = "AppBuilderWorkflow#SELECT_FORM_VIEW_BUTTON");
	}

	macro clickOnSelectObject {

		Click(locator1 = "AppBuilderWorkflow#SELECT_OBJECT_BUTTON");
	}

	macro clickOnSelectTableView {

		Click(locator1 = "AppBuilderWorkflow#SELECT_TABLE_VIEW_BUTTON");
	}

	macro selectDataAndViews {

		Click(locator1 = "AppBuilderWorkflow#DATA_AND_VIEWS_BUTTON");
	}

	macro selectFormView {

		Click(
			key_itemName = "${formViewName}",
			locator1 = "AppBuilderWorkflow#FORM_OR_TABLE_VIEW_ITEM");
	}

	macro selectObject {

		Click(
			key_objectName = "${objectName}",
			locator1 = "AppBuilderWorkflow#OBJECT_ITEM");
	}

	macro selectStartStepDataAndViews {

		AppBuilderWorkflow.selectDataAndViews();

		AppBuilderWorkflow.clickOnSelectObject();
		AppBuilderWorkflow.selectObject(objectName = "${objectName}");

		AppBuilderWorkflow.clickOnSelectFormView();
		AppBuilderWorkflow.selectFormView(formViewName = "${formViewName}");

		AppBuilderWorkflow.clickOnSelectTableView();
		AppBuilderWorkflow.selectTableView(tableViewName = "${tableViewName}");
	}

	macro selectStep {

		Click(
			key_stepNumber = "${stepNumber}",
			locator1 = "AppBuilderWorkflow#STEP_CARD");
	}

	macro selectStepAssignee {

		AppBuilderWorkflow.selectStep(stepNumber = "${stepNumber}");

		Click(locator1 = "AppBuilderWorkflow#SELECT_ASSIGNEE_FIELD");

		Click(
			key_assignee = "${assignee}",
			locator1 = "AppBuilderWorkflow#ASSIGNEE_ITEM");
	}

	macro selectStepSingleFormView {

		AppBuilderWorkflow.selectStep(stepNumber = "${stepNumber}");

		AppBuilderWorkflow.selectDataAndViews();

		if (!(IsTextEqual.isPartialText(locator1 = "AppBuilderWorkflow#SELECT_FORM_VIEW_BUTTON_TEXT", value1 = "${formViewName}"))) {
			AppBuilderWorkflow.clickOnSelectFormView();
			AppBuilderWorkflow.selectFormView(formViewName = "${formViewName}");
		}

		Click.clickNoMouseOverNoWaitForVisible(
			key_formViewMode = "${formViewMode}",
			locator1 = "AppBuilderWorkflow#FORM_VIEW_MODE");
	}

	macro selectTableView {

		Click(
			key_itemName = "${tableViewName}",
			locator1 = "AppBuilderWorkflow#FORM_OR_TABLE_VIEW_ITEM");
	}

	macro submit {

		while (IsElementNotPresent(key_text = "Submit", locator1 = "Button#ANY")) {
			Pause(locator1 = "3000");
		}
		Button.click(button = "Submit");

		AppBuilderWorkflow.waitForButtonNotPresent(buttonName = "Submit");
	}

	macro validateFormOrTableViewResultNotPresent {

		AssertElementNotPresent(
			key_itemName = "${itemName}",
			locator1 = "AppBuilderWorkflow#FORM_OR_TABLE_VIEW_ITEM");
	}

	macro validateFormOrTableViewResultPresent {

		AssertElementPresent(
			key_itemName = "${itemName}",
			locator1 = "AppBuilderWorkflow#FORM_OR_TABLE_VIEW_ITEM");
	}

	macro waitForButtonNotPresent {

		while (IsElementPresent(key_text = "${buttonName}", locator1 = "Button#ANY")) {
			Pause(locator1 = "1000");
		}
	}

}